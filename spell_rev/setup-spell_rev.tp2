BACKUP ~weidu_external\backup\spell_rev~
AUTHOR ~Bartimaeus in the SR Revised thread at forums.gibberlings3.net~
README ~spell_rev\readme-spell_rev.html~

ASK_EVERY_COMPONENT

VERSION ~v4 (Revised v1.3.966)~

ALWAYS

  OUTER_PATCH ~~ BEGIN
    PATCH_INCLUDE ~spell_rev/settings.ini~
  END
  // On EE games, convert tra files to utf-8 so that games don't crash for non-English
  ACTION_IF NOT VARIABLE_IS_SET cd_always THEN BEGIN
    OUTER_SET cd_always = 1
    ACTION_DEFINE_ARRAY cdnoconvert BEGIN setup arcane_ee divine_ee item_revisions_ee END // List of tra files that contain ONLY strings for the WeiDU installer and NOT game content
    ACTION_DEFINE_ARRAY cdreload BEGIN arcane divine END // List of tra files that contain strings used for game content that get converted in the next step
    LAF HANDLE_CHARSETS INT_VAR infer_charset = 1 STR_VAR tra_path = EVAL ~spell_rev/languages~ noconvert_array = cdnoconvert reload_array = cdreload END
  END

  ACTION_IF (GAME_IS ~tutu tutu_totsc bg2 tob bgt bgee bg2ee eet iwdee~) BEGIN
    LOAD_TRA ~spell_rev\languages\%LANGUAGE%\arcane.tra~
    LOAD_TRA ~spell_rev\languages\%LANGUAGE%\divine.tra~
    LOAD_TRA ~spell_rev\languages\%LANGUAGE%\hla.tra~
    LOAD_TRA ~spell_rev\languages\%LANGUAGE%\item_revisions.tra~
    ACTION_IF (spell_description_style=0 AND ((GAME_IS ~bgee bg2ee eet iwdee~ AND NOT GAME_IS ~tutu tutu_totsc bg2 tob bgt~) OR (MOD_IS_INSTALLED ~setup-bg2gtueestyle.tp2~ ~0~)) OR spell_description_style=2) BEGIN
      LOAD_TRA ~spell_rev\languages\%LANGUAGE%\arcane_ee.tra~
      LOAD_TRA ~spell_rev\languages\%LANGUAGE%\divine_ee.tra~
      LOAD_TRA ~spell_rev\languages\%LANGUAGE%\item_revisions_ee.tra~
    END
  END

END

////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////
////////////////////////////////////////////////////////////////////////////////////////////////////////////////

LANGUAGE ~English~
         ~english~
         ~spell_rev\languages\english\setup.tra~
		 
LANGUAGE ~Simplified Chinese~
         ~schinese~
		 ~spell_rev\languages\schinese\setup.tra~


//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/*------Spell Revisions------*/

BEGIN @10000
DESIGNATED 0
LABEL "dv-spell_rev-main"
REQUIRE_PREDICATE (ENGINE_IS ~soa tob bgee bg2ee eet iwdee~) @10102 // ~This mod is designed for the Baldur's Gate II engine and will not function on other games.~

// ToBEx
ACTION_IF install_tobex && (ENGINE_IS ~tob~) BEGIN
  OUTER_SPRINT ~TOBEX_MOD_DIRECTORY~ ~spell_rev~
  LOAD_TRA ~spell_rev/tobex_redist/TobEx.tra~
  INCLUDE ~spell_rev/tobex_redist/TobEx.tpa~
  
  COPY_EXISTING ~tobex_ini/ToBExTweak.ini~ ~tobex_ini~
    REPLACE_TEXTUALLY ~Apply Concentration Check On Damage=0~ ~Apply Concentration Check On Damage=1~
END

/*
ACTION_IF FILE_EXISTS_IN_GAME ~concentr.2da~ BEGIN // EE v2.0+
  COPY_EXISTING ~concentr.2da~ ~override~
    SET_2DA_ENTRY 2 1 2 ~1~ // enable concentration check on EE games
  BUT_ONLY
END
*/

INCLUDE ~spell_rev/lib/ds.tph~

// Actual component
INCLUDE ~spell_rev/components/main_component.tpa~

// Detectable Spells
LAF detectable_spells END

LAF detectable_spells
  INT_VAR
    append_stats = 0
    fix_khelben = 0
    fix_old_ds = 0
  STR_VAR
    table = ~spell_rev/lib/ds_sr_extra.2da~
END

// Remove Disabled Spells from Spell Selection Screens (Chargen, Sorcerer Level-Up)
ACTION_IF FILE_EXISTS ~tobex_ini/tobexcore.ini~ BEGIN
  COPY ~spell_rev/shared/hidespl_tobex.2da~ ~override/hidespl.2da~
END
ELSE ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN
  COPY ~spell_rev/shared/hidespl_bgee.2da~ ~override/hidespl.2da~
END
ELSE ACTION_IF GAME_IS ~iwdee~ BEGIN
  COPY ~spell_rev/shared/hidespl_iwdee.2da~ ~override/hidespl.2da~
END

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/*------Deva and Planetar Animations------*/

BEGIN @10001
DESIGNATED 10
LABEL "dv-spell_rev-deva_planetar_animations"
REQUIRE_PREDICATE (ENGINE_IS ~tob bgee bg2ee eet iwdee~) @10101 // ~This component requires Baldur's Gate II: Throne of Bhaal be installed.~

INCLUDE ~spell_rev/components/celestials.tpa~

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/*------Mirror Image Fix------*/

BEGIN @10002
DESIGNATED 20
LABEL "dv-spell_rev-mirror_image_fix"
REQUIRE_PREDICATE (ENGINE_IS ~soa tob bgee bg2ee eet iwdee~) @10102 // ~This mod is designed for the Baldur's Gate II engine and will not function on other games.~

INCLUDE ~spell_rev/components/mirror_image_vs_aoe.tpa~

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/*------Dispel Magic Fix------*/

BEGIN @10003
DESIGNATED 30
LABEL "dv-spell_rev-dispel_magic_fix"
REQUIRE_PREDICATE (ENGINE_IS ~soa tob bgee bg2ee eet~) @10102 // ~This mod is designed for the Baldur's Gate II engine and will not function on other games.~

INCLUDE ~spell_rev/components/dispellable_items.tpa~

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/*------NWN-style Spell Deflection------*/

BEGIN @10009
DESIGNATED 55
LABEL "dv-spell_rev-nwn_style_spell_deflection"

INCLUDE ~spell_rev/components/nwn_spell_deflection.tpa~

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/*------Update Spellbooks of Joinable NPCs------*/

BEGIN @10006
DESIGNATED 60
LABEL "dv-spell_rev-update_npc_spellbooks"
REQUIRE_PREDICATE (ENGINE_IS ~soa tob bgee bg2ee eet iwdee~) @10102 // ~This mod is designed for the Baldur's Gate II engine and will not function on other games.~
REQUIRE_COMPONENT ~setup-spell_rev.tp2~ 0 @10100 // ~This component requires that the main Spell Revisions component be installed.~

PRINT @10104 // ~Patching spellbooks of joinable NPCs, this may take a moment...~

// Generate list of joinable NPCs
INCLUDE ~spell_rev/lib/joinable_npcs.tpa~
LAM ~JOINABLE_NPC_ARRAYS~

// Remove old spells, add new ones

ACTION_IF NOT MOD_IS_INSTALLED ~Divine_Remix~ ~0~ BEGIN
  INCLUDE ~spell_rev/components/fix_divine_spellbooks.tpa~
END

INCLUDE ~spell_rev/components/fix_arcane_spellbooks.tpa~

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/*--------Revised HLAs----------------*/

BEGIN @10007
DESIGNATED 65
LABEL "dv-spell_rev-revised_hlas"
REQUIRE_PREDICATE (ENGINE_IS ~soa tob bg2ee eet~) @10102 // ~This mod is designed for the Baldur's Gate II engine and will not function on other games.~
INCLUDE ~spell_rev/lib/kreso_hla.tph~

PRINT @10105 // ~Done!~

//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\
//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\\\
////\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\//\\

/*------Revised Saving Throws------*/

/* BEGIN @10008
DESIGNATED 70
LABEL "dv-spell_rev-revised_saving_throws"
REQUIRE_PREDICATE (ENGINE_IS ~soa tob bg2ee eet~) @8996
INCLUDE ~spell_rev/components/saving_throw_tables.tpa~ */
